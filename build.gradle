plugins {
    id "com.github.dcendents.android-maven" version "1.5"

    // Plugin used to upload authenticated files to BinTray through Gradle
    id "com.jfrog.bintray" version "1.7.3"
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 1
        versionName "0.0.2"

    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
}

group = 'dk.youtec'
project.archivesBaseName = 'remoteconfiginresources'
version = '0.0.2'

install {
    group = "publishing"
    repositories.mavenInstaller {
        pom.project {
            packaging 'aar'
            groupId 'dk.youtec'
            artifactId 'remoteconfiginresources'
            version '0.0.2'

            scm {
                connection 'https://github.com/clhols/RemoteConfigInResources.git'
                url 'https://github.com/clhols/RemoteConfigInResources'

            }
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    // jFrog plugin must be declared for this line to work
    configurations = ['archives']
    // Package info for BinTray
    pkg {
        repo = 'YouTec'
        name = 'dk.youtec.remoteconfiginresources'
        userOrg = user
        licenses = ['MIT']
        vcsUrl = 'https://github.com/clhols/RemoteConfigInResources'
        version {
            name = '0.0.2'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.google.firebase:firebase-config:10.0.1'
    compile "com.android.support:appcompat-v7:25.1.1"
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}

repositories {
    mavenCentral()
}